
&НаКлиенте
Процедура ПолучитьМакет(Команда)
	мОписаниеОповещенияЗавершениеВыбораКаталога = Новый ОписаниеОповещения("ЗавершениеВыбораКаталога", ЭтаФорма);
	мДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	мДиалогВыбораКаталога.Показать(мОписаниеОповещенияЗавершениеВыбораКаталога);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораКаталога(пРезультат, пДополнительныеПараметры) Экспорт
	Если пРезультат <> Неопределено Тогда
		НачатьПолучениеФайлаССервера(, фАдресХранилища, пРезультат[0]);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоместитьМакет(Команда)
	мОписаниеОповещенияЗавершениеПомещенияФайлов = Новый ОписаниеОповещения("ОбработкаЗавершенияПомещенияФайлов", ЭтаФорма);
	НачатьПомещениеФайла(мОписаниеОповещенияЗавершениеПомещенияФайлов);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗавершенияПомещенияФайлов(пРезультат, пАдрес, пВыбранноеИмяФайла, пДополнительныеПараметры) Экспорт
	Если пРезультат <> Неопределено И пРезультат Тогда
		Объект.ИмяФайла = Сред(пВыбранноеИмяФайла, СтрНайти(пВыбранноеИмяФайла, "\", НаправлениеПоиска.СКонца) + 1);
		Объект.ДатаИзменения = ТекущаяДата();
		мДвоичныеДанные = ПолучитьИзВременногоХранилища(пАдрес);
		фАдресХранилища = ПоместитьВоВременноеХранилище(мДвоичныеДанные, ЭтаФорма.УникальныйИдентификатор);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	мОбъект = РеквизитФормыВЗначение("Объект");
	мДанные = мОбъект.ХранилищеДанных.Получить();
	фАдресХранилища = ПоместитьВоВременноеХранилище(мДанные, ЭтаФорма.УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ЗначениеЗаполнено(фАдресХранилища) Тогда
		ТекущийОбъект.ХранилищеДанных = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(фАдресХранилища), Новый СжатиеДанных(5));
	Иначе
		ТекущийОбъект.ХранилищеДанных = Неопределено;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчистить(Команда)
	фАдресХранилища = "";
	Объект.ИмяФайла = "";
	Объект.ДатаИзменения = ТекущаяДата();
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьМакет(Команда)
	мОписаниеОповещенияЗавершениеРедактированияМакета = Новый ОписаниеОповещения("ЗавершениеРедактированияМакета", ЭтаФорма);
	мСтруктураДляФормыМакетаУпр = Новый Структура("ИмяДокумента, АдресВременногоХранилища", Объект.ИмяФайла, фАдресХранилища);
	ОткрытьФорму("ОбщаяФорма.ФормаМакетаУпр", мСтруктураДляФормыМакетаУпр, ЭтаФорма, , , , мОписаниеОповещенияЗавершениеРедактированияМакета, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактированияМакета(пРезультат, пДополнительныеПараметры) Экспорт
	//из потока получим размер данных
	//мДвоичныеДанныеМакета = ПолучитьИзВременногоХранилища(фАдресХранилища);
	//мПотокВПамяти = мДвоичныеДанныеМакета.ОткрытьПотокДляЧтения();
	//ЭтаФорма[пДополнительныеПараметры.РеквизитКомментария] = "" + Объект.ИмяФайла + ";" + мПотокВПамяти.Размер() + " байт" + ";изменено " + ТекущаяДата();
	//мПотокВПамяти.Закрыть();
КонецПроцедуры